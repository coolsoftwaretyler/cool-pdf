buildscript {
  repositories {
    google()
    mavenCentral()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:8.12.0'
    classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:2.2.0'
  }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

// AGP 8.13+ requires compileSdk to be set early when applied at root
android {
  compileSdk 36
}

group = 'expo.modules.coolpdf'
version = '0.1.0'

// Only apply Expo plugin when building as part of an Expo app (not standalone)
def hasExpoModulesCore = findProject(':expo-modules-core') != null
if (hasExpoModulesCore) {
  def expoModulesCorePlugin = new File(project(":expo-modules-core").projectDir.absolutePath, "ExpoModulesCorePlugin.gradle")
  apply from: expoModulesCorePlugin
  applyKotlinExpoModulesCorePlugin()
  useCoreDependencies()
  useExpoPublishing()
}

// If you want to use the managed Android SDK versions from expo-modules-core, set this to true.
// The Android SDK versions will be bumped from time to time in SDK releases and may introduce breaking changes in your module code.
// Most of the time, you may like to manage the Android SDK versions yourself.
def useManagedAndroidSdkVersions = false
if (useManagedAndroidSdkVersions) {
  useDefaultAndroidSdkVersions()
} else {
  buildscript {
    // Simple helper that allows the root project to override versions declared by this library.
    ext.safeExtGet = { prop, fallback ->
      rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
    }
  }
  android {
    defaultConfig {
      minSdkVersion safeExtGet("minSdkVersion", 24)
      targetSdkVersion safeExtGet("targetSdkVersion", 36)
    }
  }
}

repositories {
  google()
  mavenCentral()
  maven { url 'https://jitpack.io' }
}

dependencies {
  // NOTE: The original repo at com.github.barteksc is abandoned by the maintainer; there will be no more updates coming from that repo.
  // The repo from zacharee is based on PdfiumAndroidKt, a much newer fork of PdfiumAndroid, with better maintenance and updated native libraries.
  implementation 'com.github.zacharee:AndroidPdfViewer:4.0.1'
  // Depend on PdfiumAndroidKt directly so this can be updated independently of AndroidPdfViewer as updates are provided.
  implementation 'io.legere:pdfiumandroid:1.0.33'
  implementation 'com.google.code.gson:gson:2.13.1'

  // Test dependencies
  testImplementation 'junit:junit:4.13.2'
  testImplementation 'io.mockk:mockk:1.13.13'
  testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.9.0'
  testImplementation 'androidx.test:core-ktx:1.6.1'
}

android {
  namespace "expo.modules.coolpdf"
  defaultConfig {
    versionCode 1
    versionName "0.1.0"
  }
  lintOptions {
    abortOnError false
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }
  kotlinOptions {
    jvmTarget = "17"
  }
  testOptions {
    unitTests {
      returnDefaultValues = true
      includeAndroidResources = true
    }
  }
}
